#!/usr/bin/gnuplot -c
#
# Example of how to plot the data generated by test_sensirion
#
# Call with the name of an output file as the arg

set title 'Temperature Humidity Plot'

set xlabel 'Time (GMT)'
set ylabel 'Temperature (degrees C)' tc lt 1
set y2label 'Humidity (percentage)' tc lt 2

set xtics out rotate by -15
set grid xtics
set xdata time
set timefmt '%s'
set format x '%Y-%m-%d %H:%M'

set ytics 5 nomirror tc lt 1
set y2tics 10 nomirror tc lt 2

set grid ytics mytics lw 1, lw 1
set autoscale fix

set key left top Left title 'Legend' box 3

set mouse mouseformat 5
set style data lines

plot \
ARG1 using 1:2  title 'Temperature',\
ARG1 using 1:3  title 'Humidity'

pause mouse close
